    Next r

    If IsVerbose() Then MsgBox "Jira facts built from " & rawTable & ".", vbInformation
    Exit Sub
Fail:
    MsgBox "Normalize issues failed: " & Err.Description, vbExclamation
End Sub

Private Function Jira_BuildHeaderMap(ByVal lo As ListObject) As Object
    Dim idx As Object: Set idx = CreateObject("Scripting.Dictionary")
    Dim names As Object: Set names = CreateObject("Scripting.Dictionary")
    Dim i As Long
    For i = 1 To lo.ListColumns.Count
        names(Norm(lo.ListColumns(i).Name)) = i
    Next i
    ' map keys
    Call MapCol(idx, names, "IssueKey", Array("issue key","key"))
    Call MapCol(idx, names, "Summary", Array("summary","title"))
    Call MapCol(idx, names, "IssueType", Array("issue type","type"))
    Call MapCol(idx, names, "Status", Array("status"))
    ' Map Epic from modern and legacy exports: Parent (new) or Epic Link (classic)
    Call MapCol(idx, names, "Epic", Array("parent","parent link","parent id","parent key","epic link","epic"))
    Call MapCol(idx, names, "Created", Array("created","created date","created on"))
    Call MapCol(idx, names, "Resolved", Array("resolved","resolved date","done date"))
    Call MapCol(idx, names, "StoryPoints", Array("story points","story point","story point estimate","custom field (story points)"))
    Call MapCol(idx, names, "FixVersion", Array("fix version/s","fix version"))
    Call MapCol(idx, names, "StartProgress", Array("start progress","started","in progress","in progress date","start date"))
    If idx.Count = 0 Then Set idx = Nothing
    Set Jira_BuildHeaderMap = idx
End Function

Private Sub MapCol(ByVal idx As Object, ByVal names As Object, ByVal key As String, ByVal candidates As Variant)
    Dim j As Long
    For j = LBound(candidates) To UBound(candidates)
        Dim k As String: k = Norm(CStr(candidates(j)))
        Dim col As Variant
        col = FindByContains(names, k)
        If Not IsEmpty(col) Then idx(key) = CLng(col): Exit Sub
    Next j
End Sub

